set fallback

# Lists all available recipes
@default:
  just --list

# Formats the solution
format: _restore-tools
  find -type f -name '*.cs' -not -path '*/obj/*' -not -path '*/bin/*' | \
  xargs --replace dotnet-code-butler {} --no-sort-members-by-alphabet
  dotnet-csharpier .

# Restores NuGet packages
restore:
  dotnet restore

# Builds the solution
build: restore
  dotnet build

# Sets Shelly integrator tag as user secret.
set-secret-shelly-integrator-tag tag: (_set-secret 'Integrations:Shelly:IntegratorTag' tag)

# Sets Shelly integrator token as user secret.
set-secret-shelly-integrator-token token: (_set-secret 'Integrations:Shelly:IntegratorToken' token)

# Sets Shelly callback base url (e.g. https://example.org/api).
set-secret-shelly-callback-base-url url: (_set-secret 'Integrations:Shelly:CallbackBaseUrl' url)

# Sets Sentry DSN.
set-secret-sentry-dsn dsn: (_set-secret 'Sentry:Dsn' dsn)

# Sets BetterStack source token.
set-secret-betterstack-token token: (_set-secret 'BetterStack:SourceToken' token)

# Clears all user secrets:
clear-secrets:
  dotnet user-secrets clear \
    --project 'src/OpenEMS.Server/OpenEMS.Server.csproj'

# Adds a new EF Core migration
add-migration name: _restore-tools
  dotnet ef migrations add {{name}} \
    --startup-project 'src/OpenEMS.Server/OpenEMS.Server.csproj' \
    --project 'src/OpenEMS.Infrastructure/OpenEMS.Infrastructure.csproj' \
    --output-dir 'Persistence/EntityFramework/Migrations'
  dotnet format 'src/OpenEMS.Infrastructure/OpenEMS.Infrastructure.csproj' \
    --include 'src/OpenEMS.Infrastructure/Persistence/EntityFramework/Migrations/'

# Runs the server and watches for changes
run:
  dotnet watch --project 'src/OpenEMS.Server/OpenEMS.Server.csproj'

_restore-tools:
  dotnet tool restore

# Secrets are stored at
# ~/.microsoft/usersecrets/<user_secrets_id>/secrets.json
_set-secret key value:
  dotnet user-secrets set '{{key}}' '{{value}}' \
    --project 'src/OpenEMS.Server/OpenEMS.Server.csproj'

